// Copyright 1998-2019 Epic Games, Inc. All Rights Reserved.

/*=============================================================================================
	RectLight.usf: Light sampling functions for Rect light implementation
===============================================================================================*/

#pragma once

#include "PathTracingLightCommon.ush"

void RectLight_GenerateLightRay(
	RayDesc Ray,
	int LightId,
	float3 LightUV,
	out RayDesc OutLightRay
)
{
	OutLightRay.Origin = Ray.Origin;
	OutLightRay.TMin = 0.001;
	// Get rect parameters to determine 3D direction.
	float2 DeltaP = float2(GetWidth(LightId), GetHeight(LightId)) * LightUV.xy;
	float3 LightDir = GetPosition(LightId) + DeltaP.x * GetdPdu(LightId) + DeltaP.y * GetdPdv(LightId) - Ray.Origin;

	OutLightRay.Direction = normalize(LightDir);
	OutLightRay.TMax = length(LightDir);
}

void RectLight_PdfLight(
	RayDesc Ray,
	int LightId,
	float3 LightUV,
	out float OutPdf
)
{
	// TODO: Consider making RectLight a physical light. For now, it is analytical only.
	OutPdf = 0.0;
}

void RectLight_EvalLight(
	int LightId,
	float3 LightUV,
	RayDesc Ray,
	out float3 OutRadiance
)
{
	float3 Color = GetColor(LightId);
	OutRadiance = Color;
}

void RectLight_SampleLight(
	RayDesc Ray,
	FMaterialClosestHitPayload Payload,
	float4 RandSample,
	int LightId,
	out float3 OutLightUV,
	out float OutPdf
)
{
	float3 WorldPosition = Payload.WorldPos;
	float3 LightPosition = GetPosition(LightId);
	float3 LightNormal = GetNormal(LightId);
	float3 LightdPdu = GetdPdu(LightId);
	float3 LightdPdv = GetdPdv(LightId);
	float LightWidth = GetWidth(LightId);
	float LightHeight = GetHeight(LightId);

	// Pick a random position
	float2 UV = RandSample.yz - 0.5;
	OutLightUV = float3(UV, 0);

	// Map sample point to quad
	float3 LightSamplePosition = LightPosition + LightdPdu * LightWidth * UV.x + LightdPdv * LightHeight * UV.y;
	float3 LightDirection = normalize(LightSamplePosition - WorldPosition);

	// Light-normal culling
	if (dot(-LightDirection, LightNormal) <= 0.0)
	{
		OutPdf = 0.0;
		return;
	}

	// Define rectangle and compute solid angle
	FRect Rect;
	Rect.Origin = LightPosition - WorldPosition;
	Rect.Axis[0] = LightdPdu;
	Rect.Axis[1] = LightdPdv;
	Rect.Axis[2] = cross(LightdPdu, LightdPdv);
	Rect.Extent = float2(LightWidth, LightHeight);
	FSphericalRect SphericalRect = BuildSphericalRect(Rect);
	OutPdf = isfinite(SphericalRect.SolidAngle) ? 1.0 / SphericalRect.SolidAngle : 0.0;
}