// Copyright 1998-2019 Epic Games, Inc. All Rights Reserved.

#pragma once

#include "../PathTracing/Utilities/PathTracingRandomSequence.ush"
#include "../RectLight.ush"

bool GenerateRectLightOcclusionRay(
	float3 WorldPosition,
	float3 WorldNormal,
	float3 LightPosition,
	float3 LightNormal,
	float3 LightdPdu,
	float3 LightdPdv,
	float LightWidth,
	float LightHeight,
	float2 RandSample,
	out float3 RayOrigin,
	out float3 RayDirection,
	out float RayTMin,
	out float RayTMax,
	out float RayPdf
)
{
	// Draw random variable
	RandSample -= 0.5;

	// Map sample point to quad
	float3 LightSamplePosition = LightPosition + LightdPdu * LightWidth * RandSample.x + LightdPdv * LightHeight * RandSample.y;
	float3 LightDirection = normalize(LightSamplePosition - WorldPosition);

	// Light-normal culling
	if (dot(-LightDirection, LightNormal) <= 0.0)
	{
		return false;
	}

	// Apply normal perturbation when defining ray
	RayOrigin = WorldPosition + WorldNormal * 0.01;
	RayDirection = LightDirection;
	RayTMin = 0.0;
	RayTMax = length(LightSamplePosition - WorldPosition);

	// Define rectangle
	FRect Rect;
	Rect.Origin = LightPosition - WorldPosition;
	Rect.Axis[0] = LightdPdu;
	Rect.Axis[1] = LightdPdv;
	Rect.Axis[2] = cross(LightdPdu, LightdPdv);
	Rect.Extent = float2(LightWidth, LightHeight);
	FSphericalRect SphericalRect = BuildSphericalRect(Rect);
	RayPdf = 1.0 / SphericalRect.SolidAngle;

	return true;
}
