// Copyright 1998-2018 Epic Games, Inc. All Rights Reserved.

/*=============================================================================
	SSDCommon.ush: All the code commonly shared between uniform buffers.
=============================================================================*/

#pragma once

#include "../Common.ush"
#include "../DataStructures/SceneViewFamilyBlackboard.ush"


//------------------------------------------------------- DEBUG CONFIG

/** Turn on/off tile classfication for debugging purpose. */
#define DEBUG_ENABLE_TILE_CLASSIFICATION 1

/** Should output invalid data when know there is a need. */
// TODO: there is an artifact in temporal reprojection.
#define DEBUG_WASTFUL_OUTPUT 1

/** Force the signal to a specific mip level. */
#define DEBUG_MIP_LEVEL -1

/** Debuging mode. */
	#define DEBUG_MODE_DISABLED -1
	#define DEBUG_MODE_BRUTE_FORCE_ONLY 0
	#define DEBUG_MODE_FAST_ONLY 1
#define DEBUG_MODE DEBUG_MODE_DISABLED

/** DIsable any bilateral rejection. */
#define DEBUG_DISABLE_BILATERAL 0

/** Prefer to store normalized components in internal texture. */
#define DEBUG_STORE_NORMALIZED_COMPONENTS 1


//------------------------------------------------------- COMPILER CONFIG

// Generate vector truncation warnings to errors.
#pragma warning(error: 3206)


//------------------------------------------------------- SHARED CONFIG

/** Do normal based anysotropy. */
#define CONFIG_NORMAL_ANISOTROPY 1


//------------------------------------------------------- SHARED CONSTANTS

// Maximum number of mip level supported.
#define MAX_MIP_LEVEL_COUNT 4

#define TARGETED_SAMPLE_COUNT 1024

// This constant is to artificially store the signal into higher resolution that theorically should from
// the infered bluring radius by deviding it. 1.0 means nothing happen, but in practice want this >=
// than Nyquist's 2.0 multiplier. Uses 3.0 because this is the size of the 3x3 neighborhood kernel.
#define BLURING_ESTIMATION_MULTIPLIER 3.0

// World bluring radius for a miss.
#define WORLD_RADIUS_INVALID -1
#define WORLD_RADIUS_MISS 1.#INF

// Mip level for sample that are invalid or missed.
#define MIP_LEVEL_INVALID -1
#define MIP_LEVEL_MISS 1024


//------------------------------------------------------- SHARED ENUMS

/** Light types */
	#define LIGHT_TYPE_DIRECTIONAL 0
	#define LIGHT_TYPE_POINT 1
	#define LIGHT_TYPE_SPOT 2
	#define LIGHT_TYPE_RECT 3


//------------------------------------------------------- COMMON SCENE INFOS

/** olds commonly used information of the scene of a given sample. */
struct FSSFSampleSceneInfos
{
	/** Normal of the pixel in world space. */
	float3 WorldNormal;

	/** The position of the pixel in the translated world frame. */
	float3 TranslatedWorldPosition;

	/** Pixel depth in world space. */
	float WorldDepth;
};

FSSFSampleSceneInfos CreateSampleSceneInfos()
{
	FSSFSampleSceneInfos Infos;
	Infos.WorldNormal             = 0;
	Infos.TranslatedWorldPosition = 0;
	Infos.WorldDepth              = 0;
	return Infos;
}

FSSFSampleSceneInfos InitSampleSceneInfos(
	float4x4 ScreenToTranslatedWorld,
	float2 ScreenPosition, float WorldDepth, float3 WorldNormal)
{
	FSSFSampleSceneInfos Infos;

	float2 ClipPosition = ScreenPosition * (View.ViewToClip[3][3] < 1.0f ? WorldDepth : 1.0f);
	Infos.TranslatedWorldPosition = mul(float4(ClipPosition, WorldDepth, 1), ScreenToTranslatedWorld).xyz;
	Infos.WorldDepth = WorldDepth;
	Infos.WorldNormal = WorldNormal;
	return Infos;
}

FSSFSampleSceneInfos FetchCurrentSceneInfos(float2 ScreenPosition, float2 BufferUV)
{
	FGBufferData GBufferData = GetGBufferDataFromSceneBlackboard(BufferUV);
	return InitSampleSceneInfos(
		View.ScreenToTranslatedWorld,
		ScreenPosition, GBufferData.Depth, GBufferData.WorldNormal);
}

float ProjectWorldDistanceToScreenDistance(FSSFSampleSceneInfos SceneMetadata, float WorldDistance)
{
	float4 ClipDistance = mul(View.ViewToClipNoAA, float4(WorldDistance, 0, SceneMetadata.WorldDepth, 1));
	return ClipDistance.x / ClipDistance.w;
}

float ProjectWorldDistanceToPixelDistance(FSSFSampleSceneInfos SceneMetadata, float WorldDistance)
{
	return WorldDistance * View.ViewSizeAndInvSize.x * 0.5 / (SceneMetadata.WorldDepth * GetTanHalfFieldOfView().x);
}

/** Compute the anisotropy value. The lower the value is, the more anysotropic the sample is. */
float ComputeAnisotropyInvFactor(FSSFSampleSceneInfos SceneMetadata)
{
	if (!CONFIG_NORMAL_ANISOTROPY)
	{
		return 1;
	}

	float Anysotropy = abs(dot(SceneMetadata.WorldNormal, View.ViewForward));

	return max(Anysotropy, rcp(8));
}

/** Return whether this signal should store in given mip level. */
float ComputeSignalMipLevel(FSSFSampleSceneInfos SceneMetadata, float WorldBluringRadius)
{
	if (WorldBluringRadius == WORLD_RADIUS_INVALID)
	{
		return MIP_LEVEL_INVALID;
	}
	else if (WorldBluringRadius == WORLD_RADIUS_MISS)
	{
		return MIP_LEVEL_MISS;
	}
	else if (DEBUG_MIP_LEVEL != MIP_LEVEL_INVALID)
	{
		return DEBUG_MIP_LEVEL;
	}

	// Compute how much the pixel can be blured on screen
	// TODO: anysotopy with scene bent normals?
	float SmallestPixelBlurRadius = ProjectWorldDistanceToPixelDistance(SceneMetadata, WorldBluringRadius);

	// Take into account the spatial bluring radius that may also happen when not enough temporal samples.
	SmallestPixelBlurRadius *= rcp(BLURING_ESTIMATION_MULTIPLIER);

	// Compute on what mip level this signal can be denoised.
	return clamp(log2(SmallestPixelBlurRadius), 0, MAX_MIP_LEVEL_COUNT - 1);
}

/** Returns the radius of a pixel in world space. */
// TODO: move that to CPU
float ComputePixelWorldRadius(FSSFSampleSceneInfos SceneMetadata)
{
	// Should be multiplied 0.5* for the diameter to radius, and by 2.0 because GetTanHalfFieldOfView() cover only half of the pixels.
	return (View.ViewSizeAndInvSize.z * GetTanHalfFieldOfView().x) * SceneMetadata.WorldDepth;
}

/** Convert to bilateral world distance. */
float WorldBluringRadiusToBilateralWorldDistance(float WorldBluringRadius)
{
	float Multiplier = 1;

	// The distance between two pixel is 2 times the radius of the pixel.
	Multiplier *= 2;

	// Need to take into account the furthearest pixel of 3x3 neighborhood.
	Multiplier *= sqrt(2);

	// Can take into account how much the signal is getting stored into higher resolution.
	Multiplier *= BLURING_ESTIMATION_MULTIPLIER;

	return WorldBluringRadius * Multiplier;
}


//------------------------------------------------------- COMMON TILE CLASSIFICATION

Texture2D<uint> TileClassificationTexture;

#define TILE_MODE_BRUTE_FORCE 0
#define TILE_MODE_FAST 1
#define TILE_MODE_FULLY_OCCLUDED 2
#define TILE_MODE_FULLY_LIGHTED 3
#define TILE_MODE_FULLY_INVALID 4

#define TILE_MODE_FULLY_UNSURE 5


/** Contains all the information for tile classification. */
struct FSSFTileInfos
{
	// Mode of the tile
	uint TileMode;

	// Whether a mip level should be procesed.
	bool bProcessMipLevel[MAX_MIP_LEVEL_COUNT];
};

FSSFTileInfos CreateTileInfos()
{
	FSSFTileInfos TileInfos;
	TileInfos.TileMode = TILE_MODE_BRUTE_FORCE;
	for (uint MipLevel = 0; MipLevel < MAX_MIP_LEVEL_COUNT; MipLevel++)
	{
		TileInfos.bProcessMipLevel[MipLevel] = 0;
	}
	return TileInfos;
}

FSSFTileInfos DecodeTileInfos(uint Content)
{
	FSSFTileInfos TileInfos;
	if (!DEBUG_ENABLE_TILE_CLASSIFICATION)
	{
		Content = 0x78 | TILE_MODE_FAST;
	}

	TileInfos.TileMode = Content & 0x7;

	UNROLL
	for (uint MipLevel = 0; MipLevel < MAX_MIP_LEVEL_COUNT; MipLevel++)
	{
		TileInfos.bProcessMipLevel[MipLevel] = (Content & (8 << MipLevel)) != 0;
	}
	return TileInfos;
}

FSSFTileInfos LoadTileInfos(uint2 TilePos)
{
	return DecodeTileInfos(TileClassificationTexture[TilePos]);
}

void WriteTileInfos(RWTexture2D<uint> TileClassificationOutput, uint2 TilePos, FSSFTileInfos TileInfos)
{
	uint Content = TileInfos.TileMode;

	UNROLL
	for (uint MipLevel = 0; MipLevel < MAX_MIP_LEVEL_COUNT; MipLevel++)
	{
		Content |= TileInfos.bProcessMipLevel[MipLevel] ? (8 << MipLevel) : 0;
	}

	TileClassificationOutput[TilePos] = Content;
}
